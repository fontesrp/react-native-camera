def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
  // The Android Gradle plugin is only required when opening the android folder stand-alone.
  // This avoids unnecessary downloads and potential conflicts when the library is included as a
  // module dependency in an application project.
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath("com.android.tools.build:gradle:7.3.1")
      //classpath("com.facebook.react:react-native-gradle-plugin")
    }
  }
}

apply plugin: 'com.android.library'

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 33)
  buildToolsVersion safeExtGet('buildToolsVersion', '33.0.0')

  defaultConfig {
    multiDexEnabled true
    minSdkVersion safeExtGet('minSdkVersion', 23)
    targetSdkVersion safeExtGet('targetSdkVersion', 33)
  }

  flavorDimensions "react-native-camera"

  productFlavors {
    general {
      dimension "react-native-camera"
    }
    mlkit {
      dimension "react-native-camera"
    }
  }

  lintOptions {
    abortOnError false
    warning 'InvalidPackage'
  }

  packagingOptions {
    exclude 'META-INF/androidx.exifinterface_exifinterface.version'
    exclude 'META-INF/proguard/androidx-annotations.pro'
  }
}

repositories {
  google()
  mavenCentral()
  maven { url "https://jitpack.io" }
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url "$rootDir/../node_modules/react-native/android"
  }
}

dependencies {
  // The version of react-native is set by the React Native Gradle Plugin
  implementation 'com.facebook.react:react-android'
  implementation 'com.facebook.react:hermes-android'

  implementation "androidx.annotation:annotation:1.6.0"
  implementation "androidx.exifinterface:exifinterface:1.3.6"
  implementation "androidx.legacy:legacy-support-v4:1.0.0"
  implementation "com.drewnoakes:metadata-extractor:2.18.0"
  implementation "com.google.android.gms:play-services-mlkit-text-recognition:16.3.0" // 19.0.0
  implementation "com.google.zxing:core:3.5.2"

  generalImplementation "com.google.android.gms:play-services-mlkit-barcode-scanning:16.2.0" // 18.3.0
  generalImplementation "com.google.android.gms:play-services-mlkit-face-detection:16.2.0" // 17.1.0

  mlkitImplementation "com.google.mlkit:barcode-scanning:16.2.0" // 17.2.0
  mlkitImplementation "com.google.mlkit:face-detection:16.1.5"
}
